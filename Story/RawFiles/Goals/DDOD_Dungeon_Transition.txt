Version 1
SubGoalCombiner SGC_AND
INITSECTION
GlobalClearFlag("DDOD_HUB_SignRoad_StartDungeon");
DB_currentRoomIdx(-1);
//DB_selectedRoomIdx(-1);
DB_currentLayoutIdx(-1);
DB_roomIsClear(0);

//[guid]
DB_transitionSigns((ITEMGUID)ITEMGUID_S_DDOD_HUB_SignRoad_DungeonStart_7db626a0-8abb-495b-899d-3c844a97143e);
DB_transitionSigns(ITEMGUID_S_DDOD_SignRoad_Forest_0_f40e8228-5d3c-4fce-ba33-fc5f252ebc3f);
DB_transitionSigns(ITEMGUID_S_DDOD_HUB_SignRoad_DungeonStart_DEBUG_a2695229-3fdb-423f-98a4-f40b35d4af8f);
DB_transitionSigns(ITEMGUID_S_DDOD_DemoHub_SignRoad_DungeonStart_6a6c6638-d84e-4be0-af0b-b5c8cc329ab2);

PROC_InitRoadSigns();
KBSECTION
IF
DialogEnded("DDOD_HUB_SignRoad_DungeonStart",_ID)
AND
GlobalGetFlag("DDOD_Dungeon_StartTransition",1)
AND
DB_DialogPlayers(_ID,_Player,_)
THEN
ReadyCheckStart(_Player,"DDOD_Dungeon_SignRoad_StartTransition");

IF
ReadyCheckPassed("DDOD_Dungeon_SignRoad_StartTransition")
AND
DB_dungeonGenerated(1)
AND
GlobalGetFlag("DDOD_Dungeon_StartTransition",1)
AND
QRY_DungeonPathSelected()
THEN
PROC_performRoomTransition();
SetStoryEvent(NULL_00000000-0000-0000-0000-000000000000,"PerformRoomTransition");

//REGION //transition procedures
//spawn room and perform transition
PROC
PROC_performRoomTransition()
AND
DB_selectedRoomIdx(_selectedRoomIdx)
AND
DB_currentRoomIdx(_currentRoomIdx)
AND
DB_roomsMap(_currentRoomIdx,_selectedRoomIdx,_costLight,_costFood)
AND
DB_currentLayoutIdx(_currentLayoutIdx)
AND
QRY_updateRoomsPath(_selectedRoomIdx)
AND
DB_playersMaxLevel(_,_maxLevel)
THEN
PROC_clearRoomSurface();
PROC_depopulateRoom();
PROC_clearCurrentRoomItems();
PROC_clearCurrentRoomCharacters();

PROC_populateRoom(_selectedRoomIdx,_maxLevel);
PROC_generateRoomTreasure(_currentLayoutIdx,_selectedRoomIdx);
PROC_teleportPlayersToRoom(_currentLayoutIdx,_selectedRoomIdx);

PROC_SetPathExistanceFlagsFalse();
PROC_clearPathRoomsStatus();
PROC_updatePathExistanceFlags(_selectedRoomIdx);

PROC_spendResourcesTransition(_costLight,_costFood);

NOT DB_selectedRoomIdx(_selectedRoomIdx);
NOT DB_currentRoomIdx(_currentRoomIdx);
DB_currentRoomIdx(_selectedRoomIdx);

PROC_forbidTransit();
AutoSave();

DialogSetVariableInt("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_RoomIdx_Current_fcf11d15-6829-42b8-9d0f-ecedc78b78c4",_selectedRoomIdx);


//REGION //Path selection procedures
QRY
QRY_DungeonPathSelected()
AND
GlobalGetFlag("DDOD_Dungeon_SignRoad_Selected_Path_0",1)
THEN
PROC_SetRoomSelectionFlagsFalse();
PROC_getSelectedRoomIdx(0);

QRY
QRY_DungeonPathSelected()
AND
GlobalGetFlag("DDOD_Dungeon_SignRoad_Selected_Path_1",1)
THEN
PROC_SetRoomSelectionFlagsFalse();
PROC_getSelectedRoomIdx(1);

QRY
QRY_DungeonPathSelected()
AND
GlobalGetFlag("DDOD_Dungeon_SignRoad_Selected_Path_2",1)
THEN
PROC_SetRoomSelectionFlagsFalse();
PROC_getSelectedRoomIdx(2);

QRY
QRY_DungeonPathSelected()
AND
GlobalGetFlag("DDOD_Dungeon_SignRoad_Selected_Path_3",1)
THEN
PROC_SetRoomSelectionFlagsFalse();
PROC_getSelectedRoomIdx(3);

PROC
PROC_SetRoomSelectionFlagsFalse()
THEN
GlobalClearFlag("DDOD_Dungeon_SignRoad_Selected_Path_0");
GlobalClearFlag("DDOD_Dungeon_SignRoad_Selected_Path_1");
GlobalClearFlag("DDOD_Dungeon_SignRoad_Selected_Path_2");
GlobalClearFlag("DDOD_Dungeon_SignRoad_Selected_Path_3");
//END_REGION

PROC
PROC_getSelectedRoomIdx((INTEGER)_selectedPathNumber)
AND
QRY_resetCounter()
AND
DB_currentRoomIdx(_currentRoomIdx)
AND
DB_roomsMap(_currentRoomIdx,_selectedRoomIdx,_costLight,_costFood)
AND
DB_counter(_counter)
AND
QRY_counter()
AND
_counter == _selectedPathNumber
THEN
DB_selectedRoomIdx(_selectedRoomIdx);

//REGION //rooms path (to exit)

QRY
QRY_updateRoomsPath((INTEGER)_roomIdx)
AND
DB_roomsPath(_roomIdx)
THEN
NOT DB_roomsPath(_roomIdx);

QRY
QRY_updateRoomsPath((INTEGER)_roomIdx)
AND
NOT DB_roomsPath(_roomIdx)
THEN
DB_roomsPath(_roomIdx);

PROC
PROC_clearRoomsPath()
AND
DB_roomsPath(_idx)
THEN
NOT DB_roomsPath(_idx);

//END_REGION
//END_REGION

//REGION //procedures of updating flags of path existance 
PROC
PROC_updatePathExistanceFlags((INTEGER)_selectedRoomIdx)
AND
QRY_resetCounter()
AND
DB_roomsMap(_selectedRoomIdx,_destRoomIdx,_costLight,_costFood)
AND
DB_counter(_counter)
AND
QRY_counter()
AND
_destRoomIdx != -1
AND
QRY_updatePathRoomStatus(_counter,_destRoomIdx)
AND
QRY_updatePathExistanceFlagsSetProc(_counter,_destRoomIdx,_costLight,_costFood)
THEN
DB_NOOP(0);

QRY
QRY_updatePathExistanceFlagsSetProc((INTEGER)_counter,(INTEGER)_roomIdx,(INTEGER)_costLight,(INTEGER)_costFood)
AND
_counter == 0
THEN
GlobalSetFlag("DDOD_Dungeon_SignRoad_Exists_Path_0");
DialogSetVariableInt("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_TravelCost_Path0_Light_55e5ef68-ddbd-447c-b61d-d905e3a7e5ff",_costLight);
DialogSetVariableInt("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_TravelCost_Path0_Food_edac8f29-2b26-4e34-8567-c0313c928e25",_costFood);
DialogSetVariableInt("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_RoomIdx_Path0_fa3e0a43-38a4-440c-8958-af37ffd97570",_roomIdx);


QRY
QRY_updatePathExistanceFlagsSetProc((INTEGER)_counter,(INTEGER)_roomIdx,(INTEGER)_costLight,(INTEGER)_costFood)
AND
_counter == 1
THEN
GlobalSetFlag("DDOD_Dungeon_SignRoad_Exists_Path_1");
DialogSetVariableInt("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_TravelCost_Path1_Light_c2df3cbb-a226-4256-9be3-7d2823eba351",_costLight);
DialogSetVariableInt("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_TravelCost_Path1_Food_8294558c-8a37-4031-a88e-7dffe07be15b",_costFood);
DialogSetVariableInt("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_RoomIdx_Path1_e51b53e5-a16c-4faf-b214-5cf689c80cdc",_roomIdx);

QRY
QRY_updatePathExistanceFlagsSetProc((INTEGER)_counter,(INTEGER)_roomIdx,(INTEGER)_costLight,(INTEGER)_costFood)
AND
_counter == 2
THEN
GlobalSetFlag("DDOD_Dungeon_SignRoad_Exists_Path_2");
DialogSetVariableInt("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_TravelCost_Path2_Light_41346e94-591c-4d1f-adb2-51127de0f95c",_costLight);
DialogSetVariableInt("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_TravelCost_Path2_Food_4d62c7f7-d9ee-4a9d-b116-2f2bb1295c17",_costFood);
DialogSetVariableInt("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_RoomIdx_Path2_1375b6c8-8f03-47b0-931d-f24ec2881038",_roomIdx);

QRY
QRY_updatePathExistanceFlagsSetProc((INTEGER)_counter,(INTEGER)_roomIdx,(INTEGER)_costLight,(INTEGER)_costFood)
AND
_counter == 3
THEN
GlobalSetFlag("DDOD_Dungeon_SignRoad_Exists_Path_3");
DialogSetVariableInt("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_TravelCost_Path3_Light_fd28a018-0622-4c71-beb5-bd10ac96f255",_costLight);
DialogSetVariableInt("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_TravelCost_Path3_Food_e6a030e2-88f8-429c-a619-11e6a42f4e33",_costFood);
DialogSetVariableInt("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_RoomIdx_Path3_ca81e96d-14fb-4ed5-a5ae-c71163ddda55",_roomIdx);

PROC
PROC_clearPathRoomsStatus()
THEN
DialogSetVariableString("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_RoomStatus_Path0_031fcd46-91e9-49bd-8ae4-e2456e229dd1","Enemy ahead");
DialogSetVariableString("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_RoomStatus_Path1_b14ca12a-a9a1-481f-964e-7896846d6d93","Enemy ahead");
DialogSetVariableString("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_RoomStatus_Path2_a605725b-a419-4934-95c4-c2489a507473","Enemy ahead");
DialogSetVariableString("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_RoomStatus_Path3_43853a82-94fd-4ea9-9595-2bebc0fcf342","Enemy ahead");

PROC
PROC_SetPathExistanceFlagsFalse()
THEN
GlobalClearFlag("DDOD_Dungeon_SignRoad_Exists_Path_0");
GlobalClearFlag("DDOD_Dungeon_SignRoad_Exists_Path_1");
GlobalClearFlag("DDOD_Dungeon_SignRoad_Exists_Path_2");
GlobalClearFlag("DDOD_Dungeon_SignRoad_Exists_Path_3");
//END_REGION

//REGION //update path room status
QRY
QRY_updatePathRoomStatus((INTEGER)_pathNumber,(INTEGER)_roomIdx)
AND
_pathNumber==0
AND
NOT DB_pawns_1(_roomIdx,_,_,_)
THEN
DialogSetVariableString("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_RoomStatus_Path0_031fcd46-91e9-49bd-8ae4-e2456e229dd1","Empty");

QRY
QRY_updatePathRoomStatus((INTEGER)_pathNumber,(INTEGER)_roomIdx)
AND
_pathNumber==1
AND
NOT DB_pawns_1(_roomIdx,_,_,_)
THEN
DialogSetVariableString("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_RoomStatus_Path1_b14ca12a-a9a1-481f-964e-7896846d6d93","Empty");

QRY
QRY_updatePathRoomStatus((INTEGER)_pathNumber,(INTEGER)_roomIdx)
AND
_pathNumber==2
AND
NOT DB_pawns_1(_roomIdx,_,_,_)
THEN
DialogSetVariableString("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_RoomStatus_Path2_a605725b-a419-4934-95c4-c2489a507473","Empty");

QRY
QRY_updatePathRoomStatus((INTEGER)_pathNumber,(INTEGER)_roomIdx)
AND
_pathNumber==3
AND
NOT DB_pawns_1(_roomIdx,_,_,_)
THEN
DialogSetVariableString("DDOD_HUB_SignRoad_DungeonStart","DDOD_Dungeon_RoomStatus_Path3_43853a82-94fd-4ea9-9595-2bebc0fcf342","Empty");

QRY
QRY_updatePathRoomStatus((INTEGER)_pathNumber,(INTEGER)_roomIdx)
THEN
DB_NOOP(0);

//END_REGION

//REGION //spend resources
PROC
PROC_spendResourcesTransition((INTEGER)_lightAmout,(INTEGER)_foodAmout)
AND
IntegerSubtract(0,_lightAmout,_negativeLightAmout)
AND
IntegerSubtract(0,_foodAmout,_negativeFoodAmout)
THEN
PROC_changeFood(_negativeFoodAmout);
PROC_changeLight(_negativeLightAmout);

//END_REGION

//REGION //allow/forbid transition
IF
CharacterDied(_char)
AND
DB_generatedRoomEnemies(_,_char)
THEN
PROC_checkPawnsAreDead();
PROC_checkAllowTransit();

//allow transit 
PROC
PROC_checkPawnsAreDead()
AND
QRY_resetRoomClear()
AND
DB_generatedRoomEnemies(_,_charGuid)
AND
CharacterIsDead(_charGuid,0)
THEN
DB_roomIsClear(0);

QRY
QRY_resetRoomClear()
THEN
DB_roomIsClear(1);

PROC
PROC_checkAllowTransit()
AND
DB_roomIsClear(1)
THEN
PROC_allowTransit();

PROC
PROC_forbidTransit()
AND
DB_currentRoomIdx(_roomIdx)
AND
DB_pawns_1(_roomIdx,_,_,_)
AND
DB_transitionSigns(_itemGuid)
THEN
ItemSetCanInteract(_itemGuid,0);

PROC
PROC_allowTransit()
AND
DB_transitionSigns(_itemGuid)
THEN
ItemSetCanInteract(_itemGuid,1);
//END_REGION

//REGION //dungeon exit and flee
IF
DialogEnded("DDOD_HUB_SignRoad_DungeonStart",_ID)
AND
DB_DialogPlayers(_ID,_Player,_)
AND
GlobalGetFlag("DDOD_Dungeon_SignRoad_Leave",1)
THEN
GlobalClearFlag("DDOD_Dungeon_SignRoad_Leave");
ReadyCheckStart(_Player,"DDOD_Dungeon_SignRoad_Leave");

IF
ReadyCheckPassed("DDOD_Dungeon_SignRoad_Leave")
THEN
PROC_softExitDungeon();

IF
CharacterTeleportToWaypoint(_Char,_)
AND
DB_dungeonGenerated(1)
AND
DB_currentRoomIdx(_roomIdx)
AND
NOT DB_pawns_1(_roomIdx,_,_,_)
THEN
PROC_softExitDungeon();


IF
CharacterTeleportToFleeWaypoint(_Char,_)
AND
CharacterIsPlayer(_Char,1)
THEN
PROC_hardExitDungeon();

IF
CharacterTeleportToWaypoint(_Char,_)
AND
DB_dungeonGenerated(1)
AND
DB_currentRoomIdx(_roomIdx)
AND
DB_pawns_1(_roomIdx,_,_,_)
THEN
PROC_hardExitDungeon();

PROC
PROC_exitDungeon((CHARACTERGUID)_char,(GUIDSTRING)_exitTrigger)
AND
DB_currentRoomIdx(_currentRoomIdx)
AND
DB_currentLayoutIdx(_currentLayoutIdx)
THEN
SetOnStage(_char,1);
TeleportTo(_char,_exitTrigger,"CharExitDungeon",0,0);
//CharacterTeleportPartiesToTrigger(TRIGGERGUID_S_DDOD_DemoHub_PlayerReturnTrigger_67eece7e-8401-4e12-af7f-8ddbbc2681a1,"DDOD_ExitDungeon");
PROC_clearCurrentRoomItems();
PROC_clearCurrentRoomCharacters();
PROC_clearRoomsData();
PROC_clearPawnsData();
NOT DB_currentRoomIdx(_currentRoomIdx);
DB_currentRoomIdx(-1);
NOT DB_currentLayoutIdx(_currentLayoutIdx);
DB_currentLayoutIdx(-1);
PROC_SetPathExistanceFlagsFalse();
GlobalClearFlag("DDOD_HUB_SignRoad_StartDungeon");
PROC_allowTransit();
AutoSave();

//leave
PROC
PROC_softExitDungeon()
AND
DB_PlayersCharacters_Active(_char)
THEN
SetStoryEvent(ITEMGUID_S_DDOD_DemoHub_SignRoad_DungeonStart_6a6c6638-d84e-4be0-af0b-b5c8cc329ab2,"DDOD_softExitDungeon");
PROC_exitDungeon(_char,TRIGGERGUID_S_DDOD_DemoHub_PlayerReturnTrigger_67eece7e-8401-4e12-af7f-8ddbbc2681a1);

//flee
PROC
PROC_hardExitDungeon()
AND
DB_PlayersCharacters_Active(_char)
THEN
//Moral down + half the light and supplies is left 
SetStoryEvent(ITEMGUID_S_DDOD_DemoHub_SignRoad_DungeonStart_6a6c6638-d84e-4be0-af0b-b5c8cc329ab2,"DDOD_hardExitDungeon");
PROC_changeMoral(-2);
PROC_leaveResources();
PROC_exitDungeon(_char,TRIGGERGUID_S_DDOD_DemoHub_PlayerReturnTrigger_67eece7e-8401-4e12-af7f-8ddbbc2681a1);

//END_REGION

PROC
PROC_InitRoadSigns()
AND
DB_transitionSigns(_itemguid)
THEN
DB_Dialogs(_itemguid,"DDOD_HUB_SignRoad_DungeonStart");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__Start"
